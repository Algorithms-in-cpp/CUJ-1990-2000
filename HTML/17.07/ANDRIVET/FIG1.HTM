<HTML><HEAD>
<TITLE>July 1999/A Simple XML Parser/Figure 1</TITLE></HEAD>

<BODY BACKGROUND="" BGCOLOR="#FFFFFF" TEXT="#000000">

<h4><A NAME="heading1">Figure 1: Simple XML parser grammar represented in EBNF form</A></h4>
<pre>
document     ::= XMLDecl? Misc* element Misc*
XMLDecl      ::= '&lt;?xml' VersionInfo EncodingDecl? S? '?&gt;'
     
VersionInfo  ::= S 'version' Eq 
                 (' VersionNum ' | " VersionNum ")
Eq           ::= S? '=' S?
VersionNum   ::= ([a-zA-Z0-9_.:] | '-')+
     
EncodingDecl ::=  S 'encoding' Eq 
                  ('"' EncName '"' |  "'" EncName "'")
EncName      ::=  [A-Za-z] ([A-Za-z0-9._] | '-')*
     
S            ::= (#x20 | #x9 | #xD | #xA)+ 
Misc         ::= Comment | S
Comment      ::= '&lt;!--' ((Char - '-') | ('-' (Char - '-')))* 
                 '--&gt;'
     
Name         ::=  (Letter | '_' | ':') (NameChar)* 
NameChar     ::=  Letter | Digit | '.' | '-' | '_' | ':' |
                  CombiningChar | Extender
     
Reference    ::=  EntityRef | CharRef 
EntityRef    ::=  '&amp;' Name ';'
CharRef      ::=  '&amp;#' [0-9]+ ';' | '&amp;#x' [0-9a-fA-F]+ ';'
     
Attribute    ::=  Name Eq AttValue
AttValue     ::= '"' ([^&lt;&amp;"] | Reference)* '"'  |
                 "'" ([^&lt;&amp;'] | Reference)* "'"
     
CDSect       ::=  '&lt;![CDATA[' CData ']]&gt;' 
CData        ::=  (Char* - (Char* ']]&gt;' Char*))
     
element      ::= EmptyElemTag | STag content ETag 
STag         ::=  '&lt;' Name (S Attribute)* S? '&gt;'  
ETag         ::=  '&lt;/' Name S? '&gt;'
EmptyElemTag ::=  '&lt;' Name (S Attribute)* S? '/&gt;' 
CharData     ::=  [^&lt;&amp;]* - ([^&lt;&amp;]* ']]&gt;' [^&lt;&amp;]*) 
content      ::=  (element | CharData | Reference |
                   CDSect | Comment)*


</pre>
</BODY>
</HTML>
